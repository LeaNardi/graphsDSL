comm ::=  skip
        | comm ; comm
        | cond expr then comm else comm end
        | while expr do comm end
        | for identifier in expr do comm end
        | forNeighbors identifier in expr from expr upto expr do comm end
        | forNodes identifier in expr do comm end
        | forEdges identifier in expr do comm end
        | forIncident identifier in expr on expr do comm end
        | forComponent identifier in expr do comm end
        | visualize ( expr , expr )
        | print expr
        | identifier := expr


expr ::=  condexpr
        | logicalexp


condexpr ::= logicalexp ? expr : expr


logicalexp ::= comparisonexp
                | logicalexp && comparisonexp
                | logicalexp || comparisonexp


comparisonexp ::= arithmeticexp
                | comparisonexp == arithmeticexp
                | comparisonexp < arithmeticexp
                | comparisonexp > arithmeticexp


arithmeticexp ::= term
                | arithmeticexp + term
                | arithmeticexp - term


term ::= factor
        | term * factor
        | term / factor
        | term % factor


factor ::= number
        | boolean
        | string
        | emptyliteral
        | functioncall
        | identifier
        | graphexp
        | edgeexp
        | queueexp
        | unionfindexp
        | listexp
        | unary
        | ( expr )


number ::= integer | float

boolean ::= true | false

string ::= " [^"]* "

emptyliteral ::= emptyList | emptyQueue

identifier ::= [a-zA-Z][a-zA-Z0-9_]*

unary ::= - factor | ! factor


graphexp ::= graph [ nodelist ]


nodelist ::= nodeassoc
        | nodeassoc , nodelist
        | ε


nodeassoc ::= ( expr , [ edgelist ] )


edgelist ::= edgeassoc
        | edgeassoc , edgelist
        | ε


edgeassoc ::= ( expr , expr )


edgeexp ::= edge ( expr , expr , expr )


listexp ::= [ exprlist ]


queueexp ::= queue [ exprlist ]


unionfindexp ::= unionfind [ ufpairlist ]


ufpairlist ::= ufpair
        | ufpair , ufpairlist
        | ε


ufpair ::= ( expr , expr )


exprlist ::= expr
        | expr , exprlist
        | ε


functioncall ::= functionname ( arglist )


functionname ::= addNode | deleteNode | addEdge | deleteEdge
        | graphComplement | graphUnion | graphIntersection | getEdges | getNodes | adjacentNodes | adjacentEdges
        | getWeight | getNode1 | getNode2
        | lenList | appendList | consList | concatList | headList | lastList | tailList | initList | reverseList | sortByWeight
        | queueLen | enqueue | dequeue | peek | isEmpty
        | union | find
        | esCiclico | esConexo | inList | isEmptyList
        | metricClosure | metricClosurePaths
        | getConnectedComponents
        | getNodeMap
        | getNodesMap
        | getValue
        | setValue

arglist ::= expr
        | expr , arglist
        | ε
